--$Header: /home/vdmtools/cvsroot/toolbox/examples-jp/pp/ModelingSystemByJapanese/examples/explo1.vpp,v 1.1 2006/01/04 06:44:17 vdmtools Exp $
--第６章の爆発物貯蔵庫コントローラー
-- 最初の版: 分配演算子は使用せず

types

点型 :: x : nat
         y : nat;

オブジェクト型 :: 位置 : 点型
          x長  : nat
          y長  : nat;

貯蔵庫型 :: 内容物群 : set of オブジェクト型
         x限度   : nat
         y限度   : nat
inv mk_貯蔵庫型(内容物群, x限度, y限度) ==

-- 1. オブジェクトは貯蔵庫の範囲内にある
   (forall 物 in set 内容物群 & 限度内？(物,x限度,y限度))
   and 

-- 2. ２つの異なるオブジェクトは重ならない
   not exists 物1, 物2 in set 内容物群 & 物1 <> 物2 and 重複？(物1,物2)


   
functions

-- 不変条件のための補助関数

-- 与えられたオブジェクトの位置は、与えられたx限度とy限度の内にあるか？

限度内？ : オブジェクト型 * nat * nat -> bool
限度内？(ある物,x限度,y限度) == 
   ある物.位置.x + ある物.x長 <= x限度 and 
   ある物.位置.y + ある物.y長 <= y限度;

-- ２つのオブジェクトが重なるか？

重複？ : オブジェクト型 * オブジェクト型 -> bool
重複？(ある物1,ある物2) ==
   点の集合(ある物1) inter 点の集合(ある物2) <> {}; 

-- オブジェクト内の点の集合

点の集合 : オブジェクト型 -> set of 点型
点の集合(mk_オブジェクト型(ある位置,あるx長,あるy長)) == 
   {mk_点型(x,y) | x in set {ある位置.x ,..., ある位置.x + あるx長},
                    y in set {ある位置.y ,..., ある位置.y + あるy長}};


-- 主な機能

-- 貯蔵庫中のオブジェクト数 

オブジェクト数 : 貯蔵庫型 -> nat
オブジェクト数(ある貯蔵庫) == card ある貯蔵庫.内容物群;

-- 与えられたオブジェクトを、与えられた位置に格納する余裕があるか,
-- 他のオブジェクトと重ならないかをチェックする。 

余地がある？ : オブジェクト型 * 貯蔵庫型 * 点型 -> bool
余地がある？(ある物,ある貯蔵庫,ある位置) == 
   let 新しい物 = mk_オブジェクト型(ある位置,ある物.x長,ある物.y長) in 
      限度内？(新しい物,ある貯蔵庫.x限度,ある貯蔵庫.y限度) and 
      not exists ある物1 in set ある貯蔵庫.内容物群 & 重複？(ある物1,新しい物);

--可能ならば、与えられたオブジェクトを置く位置を返す陰関数。
--そのような位置が無ければ、nilを返す。

位置を示唆する(ある物:オブジェクト型, ある貯蔵庫:貯蔵庫型) ある位置:[点型]
post if exists ある点:点型 & 余地がある？(ある物,ある貯蔵庫,ある点) 
     then 余地がある？(ある物,ある貯蔵庫,ある位置)
     else ある位置 = nil;

-- 与えられたオブジェクトを、与えられた貯蔵庫の与えられた位置に置く。
-- 修正された貯蔵庫を返す。 

置く : オブジェクト型 * 貯蔵庫型 * 点型 -> 貯蔵庫型
置く(ある物,ある貯蔵庫,ある位置) == 
   let 新しい物 = mk_オブジェクト型(ある位置,ある物.x長,ある物.y長) in 
   mk_貯蔵庫型(ある貯蔵庫.内容物群 union {新しい物},
            ある貯蔵庫.x限度,
            ある貯蔵庫.y限度)
pre 余地がある？(ある物,ある貯蔵庫,ある位置);

-- 与えられた貯蔵庫から、与えられた位置のオブジェクト群を除く。 
-- 修正されたた貯蔵庫を返す。

除去する : 貯蔵庫型 * set of 点型 -> 貯蔵庫型
除去する(mk_貯蔵庫型(内容物群,x限度,y限度),位置群) == 
   let オブジェクト群 = {物 |物 in set 内容物群 & 物.位置 in set 位置群} in
   mk_貯蔵庫型(内容物群 \ オブジェクト群, x限度, y限度)








