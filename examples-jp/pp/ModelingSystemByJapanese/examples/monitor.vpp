--$Header: /home/vdmtools/cvsroot/toolbox/examples-jp/pp/ModelingSystemByJapanese/examples/monitor.vpp,v 1.1 2006/01/04 06:44:17 vdmtools Exp $
-- 第４章の温度モニター（論理学）

types

  読込温度型 = seq of int
  inv 温度 == len 温度 = 5

functions

--最後に読み込んだサンプルが最初より大きい 

  上昇？: 読込温度型 -> bool
  上昇？(ある温度) ==
   ある温度(1) < ある温度(5);

-- 400度を超えるものを読み込む

  限度超過？: 読込温度型 -> bool
  限度超過？(ある温度) ==
    ある温度(1) > 400 or ある温度(2) > 400 or 
    ある温度(3) > 400 or ある温度(4) > 400 or 
    ある温度(5) > 400;

-- 限量式を使った定式化（代替案）

  限度超過？2: 読込温度型 -> bool
  限度超過？2(ある温度) ==
    exists i in set inds ある温度 & ある温度(i) > 400;

-- 読込サンプルすべてが４００度超過

  継続的限度超過？: 読込温度型 -> bool
  継続的限度超過？(ある温度) ==
    ある温度(1) > 400 and ある温度(2) > 400 and 
    ある温度(3) > 400 and ある温度(4) > 400 and 
    ある温度(5) > 400;

-- 限量式を使った定式化（代替案）

  継続的限度超過？2: 読込温度型 -> bool
  継続的限度超過？2(ある温度) ==
    forall i in set inds ある温度 & ある温度(i) > 400;

-- 反応装置が安全か検出する

  安全？: 読込温度型 -> bool
  安全？(ある温度) ==
    ある温度(3) > 400 => ある温度(5) < 400;

-- 警報を上げるべきか検出する

  警報を上げる？(ある温度: 読込温度型) 警報 : bool
  post not 安全？(ある温度) <=> 警報
