--$Header: /home/vdmtools/cvsroot/toolbox/examples-jp/pp/ModelingSystemByJapanese/examples/traffic.vpp,v 1.1 2006/01/04 06:44:17 vdmtools Exp $
-- 第５章（基本要素）の交通信号機カーネル
-- タイミングの制約を考慮しない最初の版

values

-- 以下の値定義は図5.2の交差点の表現を構築するのに使用される

  p1 : 方向型 = mk_token("A1北");

  p2 : 方向型 = mk_token("A1南");

  p3 : 方向型 = mk_token("A66東");

  p4 : 方向型 = mk_token("A66西");

  信号写像 : map 方向型 to 信号型
         = {p1 |-> <赤>,
            p2 |-> <赤>,
            p3 |-> <青>,
            p4 |-> <青>};

  交差点 : set of 交差型
            = {mk_交差型(p1,p3),
               mk_交差型(p1,p4),
               mk_交差型(p2,p3),
               mk_交差型(p2,p4),
               mk_交差型(p3,p1),
               mk_交差型(p4,p1),
               mk_交差型(p3,p2),
               mk_交差型(p4,p2)};

  カーネル : カーネル型 
         = mk_カーネル型(信号写像,交差点)

types

  信号型 = <赤> | <黄> | <青>;

  時間型 = real
  inv 時間 == 時間 >= 0;

  方向型 = token;
  
  交差型 :: 方向1: 方向型
              方向2: 方向型
  inv mk_交差型(p1,p2) == p1 <> p2;

-- カーネルデータ構造は２つの構成要素を持つ。
-- 1) 各方向の信号の現在状態を持つ写像
-- 2) 交差点の集合

  カーネル型 :: 信号写像    : map 方向型 to 信号型
            交差点群 : set of 交差型
  inv mk_カーネル型(ある信号写像,ある交差点群) ==
        forall 交差点 in set ある交差点群 & 
              mk_交差型(交差点.方向2,交差点.方向1) in set ある交差点群 and
              交差点.方向1 in set dom ある信号写像 and 
	      交差点.方向2 in set dom ある信号写像 and 
              (ある信号写像(交差点.方向1) = <赤> or ある信号写像(交差点.方向2) = <赤>)

functions

-- 与えられた方向の信号を青にする

  青にする: 方向型 * カーネル型 -> カーネル型
  青にする(ある方向,mk_カーネル型(信号写像,交差点群)) ==
    mk_カーネル型(信号を変える(信号写像,ある方向,<青>),交差点群)
  pre ある方向 in set dom 信号写像 and
      信号写像(ある方向) = <赤> and
      forall mk_交差型(p1,p2) in set 交差点群 &
             (ある方向 = p1 => 信号写像(p2) = <赤>);

-- 与えられた方向の信号を赤にする

  赤にする: 方向型 * カーネル型 -> カーネル型
  赤にする(ある方向,mk_カーネル型(信号写像,交差点群)) ==
    mk_カーネル型(信号を変える(信号写像,ある方向,<赤>),交差点群)
  pre ある方向 in set dom 信号写像 and 信号写像(ある方向) = <黄>;

-- 与えられた方向の信号を黄にする

  黄にする: 方向型 * カーネル型 -> カーネル型
  黄にする(ある方向,mk_カーネル型(信号写像,交差点群)) ==
    mk_カーネル型(信号を変える(信号写像,ある方向,<黄>),交差点群)
  pre ある方向 in set dom 信号写像 and 信号写像(ある方向) = <青>;

  信号を変える: (map 方向型 to 信号型) * 方向型 * 信号型 -> (map 方向型 to 信号型)
  信号を変える(ある信号写像,ある方向,ある信号) ==
    ある信号写像 ++ {ある方向 |-> ある信号}

-- 時間の要素を考慮に入れる拡張は、ファイルtraffic2.vdmに記述した
