%--------------------------------------------------------------------------------
% WHAT
%    Abstract Syntax for the Static Semantics of VDM++ and VDM-SL.
%      C preprocessor macroes:
%        VDMPP  -> VDM++ abstract syntax.
%          -> VDM-SL abstract syntax.
%        TPINFO -> Type information used by the code generator.
% $Id: common_as.vdm,v 1.74 2006/03/15 07:53:31 vdmtools Exp $
% Implemented in: ???
%--------------------------------------------------------------------------------




\section{VDM-SL Abstract Syntax}
\label{sec:as}

\begin{vdm_al}
module AS

imports
  from REP all,
  from CI all

exports all

definitions

types

VdmFiles = map CI`FileId to SpecFile;

SpecFile :: name      : [FileName]
            vdm       : Document
            tokenci   : CI`TokenContextInfo
            timestamp : [TimeStamp];

TimeStamp = token;

FileName = seq1 of char;


\end{vdm_al}

\subsection{Trace Definitions}

\begin{vdm_al}
types
TraceDefList = seq of TraceDefTerm;

TraceDefTerm = TraceDef | TraceDefAltn;

TraceDef = SimpleTrace | QualifiedTrace | RepeatTrace | QualifiedRepeatTrace;

TraceDefAltn :: term : TraceDefTerm
                tdef : TraceDef
                cid :- CI`ContextId
                ;

SimpleTrace = TraceApplyExpr | TraceBracketedExpr | TracePermuteExpr;

TraceApplyExpr :: op   : Name
                  args : seq of Expr
                  cid :- CI`ContextId
                  ;

TraceBracketedExpr :: list : TraceDefList
                      cid :- CI`ContextId
                      ;

TracePermuteExpr :: list : TraceDefList
                    cid :- CI`ContextId
                    ;

QualifiedTrace :: binds : seq1 of TraceBind
                  tdef  : SimpleTrace
                  cid :- CI`ContextId
                  ;

TraceBind = LocalTraceBind | LetTraceBind | LetBeTraceBind;

LocalTraceBind :: localdefs : seq1 of LocalDef
                  cid :- CI`ContextId
                  ;

LetTraceBind :: bind : BindList
                cid :- CI`ContextId
                ;

LetBeTraceBind :: bind   : BindList
                  stexpr : [Expr]
                  cid :- CI`ContextId
                  ;

RepeatTrace :: tdef : SimpleTrace
               pat  : TraceRepeatPattern
               cid :- CI`ContextId
               ;

TraceRepeatPattern = <ANY> | <ATLEASTONE> | <POSSIBLY> | RepeatFixed | RepeatInterval;

RepeatFixed :: num : NumLit
               cid :- CI`ContextId
               ;

RepeatInterval :: low  : NumLit
                  high : NumLit
                  cid :- CI`ContextId
                  ;

QualifiedRepeatTrace :: binds : seq1 of TraceBind
                        tdef  : SimpleTrace
                        pat   : TraceRepeatPattern
                        cid :- CI`ContextId
                        ;

\end{vdm_al}

\subsection{Document}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Document -----------------------------------
-----------------------------------------------------------------------

Document = seq of (Module | DLModule) | seq of Definitions
inv d == (exists x in set elems d & is_Definitions (x)) => (len d = 1) ;

-----------------------------------------------------------------------
--------------------------- Modules -----------------------------------
-----------------------------------------------------------------------

Module :: nm   : Name
          intf : Interface
          defs : [Definitions]
          cid :- CI`ContextId
          ;

Interface :: imp  : map Name to [ImportSig]
             exp  : [ExportSig]
             cid :- CI`ContextId
             ;

ImportSig :: tps : map Name to [TypeDef]
             val : map Name to [Type]
             fns : map Name to [(TypeVarList * FnType)]
             ops : map Name to [OpType]
             ren : map Name to Name
             cid :- CI`ContextId
             ;

ExportSig :: tps : map Name to [<STRUCT>]
             val : map Name to Type
             fns : map Name to (TypeVarList * FnType) --
             ops : map Name to OpType
             cid :- CI`ContextId
             ;

-----------------------------------------------------------------------
----------------- DL Modules ------------------------------
-----------------------------------------------------------------------

DLModule :: nm     : Name
            intf   : DLInterface
            useslib : [TextLit]
            cid :- CI`ContextId
            ;

DLInterface :: imp  : map Name to DLImportSig
               exp  : DLExportSig
               cid :- CI`ContextId
               ;

DLImportSig :: tps : map Name to [ImportSig]
inv imp == (forall impsig in set rng imp.tps & impsig = nil) ;

DLExportSig :: val : map Name to Type
               fns : map Name to FnType
               ops : map Name to OpType
               cid :- CI`ContextId
               ;

-----------------------------------------------------------------------
---------------------- Access specifiers ------------------------------
-----------------------------------------------------------------------

Access = <DEFAULT_AS> | <PRIVATE_AS> | <PROTECTED_AS> | <PUBLIC_AS> | <NOT_INITIALISED_AS>;

-----------------------------------------------------------------------
-------------------------- Definitions --------------------------------
-----------------------------------------------------------------------

Definitions :: typem    : map Name to TypeDef
               valuem   : seq of ValueDef
               fnm      : map Name to FnDef
               opm      : map Name to OpDef
               State    : [ StateDef ]
               tracem   : map NameList to TraceDefList
               cid :- CI`ContextId
               ;


StateDef :: tp   : CompositeType
            Inv  : [Invariant]  -- new. was: [ Pattern * Expr ]
            Init : [StateInit] -- new. was: [ Pattern * Expr ]
            cid :- CI`ContextId
            ;

StateInit ::
        pat  : Pattern
        expr : Expr
        cid :- CI`ContextId
        ;

\end{vdm_al}

The {\em StateDef} type definition is really not part of the VDM++ language, however,
the type definition is included in order to avoid too many ifdefs in the specification of the dynamic semantics.

\subsection{Types} \label{atypes}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Type Definitions ---------------------------
-----------------------------------------------------------------------

TypeDef :: nm     : Name
           shape  : Type
           Inv    : [Invariant]
           Eq     : [Equal]
           Ord    : [Order]
           access : Access
           cid :- CI`ContextId
--inv td == true -- len td.nm.ids = 1
;

Invariant ::
     pat  : Pattern
     expr : Expr
     cid :- CI`ContextId
     ;

Equal ::
     lhs  : Pattern
     rhs  : Pattern
     expr : Expr
     cid :- CI`ContextId
     ;

Order ::
     lhs  : Pattern
     rhs  : Pattern
     expr : Expr
     cid :- CI`ContextId
     ;

Type = BasicType |
       QuoteType | CompositeType | UnionType |
       ProductType | OptionalType | SetType | SeqType |
       MapType | TypeName | FnType | BracketedType |
       OpType | TypeVar | VoidType | AllType;

BracketedType :: tp : Type
                 cid :- CI`ContextId
                 ;

BasicType = BooleanType | NumericType | TokenType | CharType;

BooleanType :: cid :- CI`ContextId
               ;

NumericType :: qtp : <NATONE> | <NAT> | <INTEGER> | <RAT> | <REAL>
               cid :- CI`ContextId
               ;

TokenType :: cid :- CI`ContextId
             ;

CharType :: cid :- CI`ContextId
            ;

QuoteType :: lit : QuoteLit
             cid :- CI`ContextId
             ;

CompositeType :: name : Name
                 fields : seq of Field
                 cid :- CI`ContextId
--inv ct == true -- len ct.name. ids = 1
;
\end{vdm_al}

The entry {\em dc} in the {\em Field} type describes if the field is an abstract field.

\begin{vdm_al}
Field :: sel  : [Name]
         type : Type
         dc   : bool
         cid :- CI`ContextId
--inv field == if field.sel <> nil
--             then len field.sel.ids = 1
--             else true;
inv field == field.sel <> nil => len field.sel.ids = 1;

UnionType :: tps : seq1 of Type
             cid :- CI`ContextId
             ;

ProductType :: tps : seq1 of Type
               cid :- CI`ContextId
               ;

OptionalType :: tp: Type
                cid :- CI`ContextId
                ;

SetType = Set0Type | Set1Type;

Set0Type :: elemtp : Type
           cid :- CI`ContextId
           ;

Set1Type :: elemtp : Type
           cid :- CI`ContextId
           ;

SeqType = Seq0Type | Seq1Type;

Seq0Type :: elemtp : Type
            cid :- CI`ContextId
            ;

Seq1Type :: elemtp : Type
            cid :- CI`ContextId
            ;

MapType = GeneralMap0Type | GeneralMap1Type | InjectiveMap0Type | InjectiveMap1Type;

GeneralMap0Type :: mapdom : Type
                  maprng : Type
                  cid :- CI`ContextId
                  ;

GeneralMap1Type :: mapdom : Type
                  maprng : Type
                  cid :- CI`ContextId
                  ;

InjectiveMap0Type :: mapdom : Type
                    maprng : Type
                    cid :- CI`ContextId
                    ;

InjectiveMap1Type :: mapdom : Type
                    maprng : Type
                    cid :- CI`ContextId
                    ;

TypeName :: name : Name
            cid :- CI`ContextId
            ;

FnType = PartialFnType | TotalFnType;

PartialFnType :: fndom : DiscretionaryType
                 fnrng : Type
                 cid :- CI`ContextId
                 ;

TotalFnType :: fndom : DiscretionaryType
               fnrng : Type
               cid :- CI`ContextId
               ;

DiscretionaryType = seq of Type;

TypeVar :: name : Name
           cid :- CI`ContextId
           ;

VoidType :: cid :- CI`ContextId
            ;

AllType :: cid :- CI`ContextId
           ;

-- AllType is used by the dynamic semantics to deal with the result type
-- of lambda expressions! Otherwise it is not used!!

\end{vdm_al}

\subsection{Functions} \label{afunctions}

\begin{vdm_al}

-----------------------------------------------------------------------
-------------------------- Functions Definitions ----------------------
-----------------------------------------------------------------------

FnDef = ExplFnDef | ImplFnDef | ExtExplFnDef;

ExplFnDef :: nm      : Name
             tpparms : TypeVarList
             tp      : FnType
             parms   : ParametersList
             body    : FnBody
             fnpre   : [Expr]
             fnpost  : [Expr]
             access  : Access
             stat    : bool
             measu   : [Expr | <NOTYETSPEC>]
             cid :- CI`ContextId
inv fndef == len fndef.nm.ids = 1;

ImplFnDef :: nm       : Name
             params   : TypeVarList
             partps   : ParameterTypes
             resnmtps : seq of NameType
             fnpre    : [Expr]
             fnpost   : Expr
             access   : Access
             stat     : bool
             cid :- CI`ContextId
inv fndef == len fndef.nm.ids = 1;

ExtExplFnDef :: nm       : Name
                params   : TypeVarList
                partps   : ParameterTypes
                resnmtps : seq of NameType
                body     : FnBody
                fnpre    : [Expr]
                fnpost   : [Expr]
                access   : Access
                stat     : bool
                measu   : [Expr]
                cid :- CI`ContextId
inv fndef == len fndef.nm.ids = 1;

TypeVarList = seq of TypeVar;        -- hc: not a node (no pos info about parenthesis
ParametersList = seq1 of Parameters; -- hc: not a node (no pos info about parenthesis
Parameters = seq of Pattern;         -- hc: not a node (no pos info about parenthesis
ParameterTypes = seq of PatTypePair; -- hc: not a node (no pos info about parenthesis

NameType :: nm : Name
            tp : Type
            cid :- CI`ContextId
inv nt == len nt.nm.ids = 1;

PatTypePair :: pats : seq1 of Pattern
               tp   : Type
               cid :- CI`ContextId
               ;

NoBody = <NOTYETSPEC>;

FnBody :: body : Expr | NoBody
                 cid :- CI`ContextId
                 ;
\end{vdm_al}

\subsection{Operations}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Operations Definitions ---------------------
-----------------------------------------------------------------------

OpDef = ExplOpDef | ImplOpDef | ExtExplOpDef;
\end{vdm_al}
\begin{vdm_al}
ExplOpDef :: nm      : Name
             oppure  : bool
             opsync  : bool
             tp      : OpType
             parms   : Parameters
             body    : OpBody
             oppre   : [Expr]
             oppost  : [Expr]
             access  : Access
             stat    : bool
             constr  : bool
             cid :- CI`ContextId
             ;

\end{vdm_al}
The field $timepost$ in $ExplOpDef$ will always be {\sf nil}
in VDM-SL specifications.
\begin{vdm_al}

ImplOpDef :: nm       : Name
             oppure   : bool
             opsync   : bool
             partps   : ParameterTypes
             resnmtps : seq of NameType
             opext    : seq of ExtInf
             oppre    : [Expr]
             oppost   : Expr
             excps    : seq of Error
             access   : Access
             stat     : bool
             constr   : bool
             cid :- CI`ContextId
             ;

\end{vdm_al}
\begin{vdm_al}

ExtExplOpDef :: nm       : Name
                oppure   : bool
                opsync   : bool
                partps   : ParameterTypes
                resnmtps : seq of NameType
                body     : OpBody
                opext    : seq of ExtInf
                oppre    : [Expr]
                oppost   : [Expr]
                excps    : seq of Error
                access   : Access
                stat     : bool
                constr   : bool
                cid :- CI`ContextId
                ;

\end{vdm_al}
The field $timepost$ in $ExtExplOpDef$ will always be {\sf nil}
in VDM-SL specifications.
\begin{vdm_al}

OpType :: opdom : DiscretionaryType
          oprng : Type
          cid :- CI`ContextId
          ;

ExtInf :: mode : Mode
          vars : seq of Name
          tp   : [Type]
          cid :- CI`ContextId
          ;

Mode = <READ> | <READWRITE>;

OpBody :: body : Stmt | NoBody
                 cid :- CI`ContextId
                 ;
\end{vdm_al}

\subsection{Values} \label{avalues}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Value Definitions --------------------------
-----------------------------------------------------------------------

ValueDef :: pat    : Pattern
            tp     : [Type]
            val    : Expr
            access : Access
            stat   : bool
            cid :- CI`ContextId
            ;
\end{vdm_al}

\subsection{Expressions} \label{aexpr}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------------- Expressionts -------------------------------
-----------------------------------------------------------------------

Expr = BracketedExpr | DefExpr | LetExpr | LetBeSTExpr| IfExpr | CasesExpr |
       UnaryExpr | BinaryExpr | QuantExpr| IotaExpr |
       SetEnumerationExpr | SetComprehensionExpr | SetRangeExpr |
       SeqEnumerationExpr | SeqComprehensionExpr |
       SubSequenceExpr | SeqModifyMapOverrideExpr |
       MapEnumerationExpr | MapComprehensionExpr | Maplet |
       TupleConstructorExpr |
       RecordConstructorExpr | RecordModifierExpr |
       FieldSelectExpr | ApplyExpr |
       IsExpr | Literal | Name | OldName | UndefinedExpr |
       NarrowExpr |
       TupleSelectExpr | TypeJudgementExpr | PreConditionApplyExpr |
       TokenConstructorExpr | FctTypeInstExpr | LambdaExpr |
       LastRes | Macro;

-- only used in the concurrency module                                 -- (!) OlOpp
                                                                       -- (!) OlOpp
GuardExpr :: expr : AS`Expr  -- this is the original guard expression  -- (!) OlOpp
             name : AS`Name; -- this is the name of the class,         -- (!) OlOpp
                             --  in which the expression was defined.  -- (!) OlOpp

BracketedExpr :: expr  : Expr
                 cid :- CI`ContextId

                 ;

DefExpr :: Def : seq1 of (PatternBind * Expr)
           In  : Expr
           cid :- CI`ContextId

           ;

LetExpr :: localdef : seq1 of LocalDef
           body     : Expr
           cid :- CI`ContextId

           ;

LocalDef = FnDef | ValueDef;

LetBeSTExpr :: lhs : BindList
               St  : [Expr]
               In  : Expr
               cid :- CI`ContextId

               ;

IfExpr :: test   : Expr
          cons   : Expr
          elsif  : seq of ElseifExpr
          altn   : Expr
          cid :- CI`ContextId

;

ElseifExpr :: test : Expr
              cons : Expr
              cid :- CI`ContextId
              ;

CasesExpr :: sel    : Expr
             altns  : seq1 of CaseAltn
             Others : [ Expr ]
             cid :- CI`ContextId

;

CaseAltn :: match : seq1 of Pattern
            body  : Expr
            cid :- CI`ContextId
            ;

UnaryExpr = PrefixExpr;

PrefixExpr :: opr   : UnaryOp
              arg   : Expr
              cid :- CI`ContextId

;

UnaryOp = <NUMPLUS> | <NUMMINUS> | <NUMABS> | <FLOOR> |
          <NOT> |
          <SETCARD> | <SETDISTRUNION> | <SETDISTRINTERSECT> | <SETPOWER> |
          <SEQLEN> | <SEQDISTRCONC> | <SEQELEMS> | <SEQINDICES> |
          <SEQTAIL> | <SEQHEAD> | <SEQREVERSE> |
          <MAPDOM> | <MAPRNG> | <MAPDISTRMERGE> | <MAPINVERSE>;

BinaryExpr :: left  : Expr
              opr   : BinaryOp
              right : Expr
              cid :- CI`ContextId

;

BinaryOp = <NUMPLUS> | <NUMMINUS> | <NUMMULT> | <NUMDIV> |
           <NUMREM> | <NUMMOD> | <NUMEXP> | <INTDIV> |
           <NUMLT> | <NUMLE> | <NUMGT> | <NUMGE> |
           <AND> | <OR> | <IMPLY> | <EQUIV> |
           <EQ> | <NE> |
           <SETUNION> | <SETINTERSECT> | <SETMINUS> | <SUBSET> |
           <PROPERSUBSET> | <INSET> | <NOTINSET> |
           <SEQCONC> |
           <MAPMERGE> | <MAPDOMRESTTO> | <MAPDOMRESTBY> |
           <MAPRNGRESTTO> | <MAPRNGRESTBY> |
--           <COMPOSE>;
           <COMPOSE> | ( <LEXORD> * nat );

QuantExpr = AllOrExistsExpr | ExistsUniqueExpr;

AllOrExistsExpr :: quant : AllOrExistsQuantifier
                   bind  : BindList
                   pred  : Expr
                   cid :- CI`ContextId

                   ;

ExistsUniqueExpr :: bind : Bind
                    pred : Expr
                    cid :- CI`ContextId

                    ;

AllOrExistsQuantifier = <ALL> | <EXISTS>;

IotaExpr :: bind : Bind
            pred : Expr
            cid :- CI`ContextId

;

SetEnumerationExpr :: els : seq of Expr
                      cid :- CI`ContextId

;

SetComprehensionExpr :: elem : Expr
                        bind : BindList
                        pred : [Expr]
                        cid :- CI`ContextId

                        ;

SetRangeExpr :: lb : Expr
                ub : Expr
                cid :- CI`ContextId

                ;

SeqEnumerationExpr :: els : seq of Expr
                      cid :- CI`ContextId

;

SeqComprehensionExpr :: elem : Expr
                        bind : SetBind | SeqBind
                        pred : [Expr]
                        cid :- CI`ContextId

;

SubSequenceExpr :: sequence : Expr
                   frompos  : Expr
                   topos    : Expr
                   cid :- CI`ContextId

;

SeqModifyMapOverrideExpr :: seqmap : Expr
                            mapexp : Expr
                            cid :- CI`ContextId

;

MapEnumerationExpr :: els : seq of Maplet
                      cid :- CI`ContextId

;

Maplet :: mapdom : Expr
          maprng : Expr
          cid :- CI`ContextId
;

MapComprehensionExpr :: elem : Maplet
                        bind : BindList
                        pred : [Expr]
                        cid :- CI`ContextId

;

TupleConstructorExpr :: fields : seq1 of Expr
                        cid :- CI`ContextId

;

TokenConstructorExpr :: field : Expr
                        cid :- CI`ContextId

;

RecordConstructorExpr :: tag: Name
                         fields: seq of Expr
                         cid :- CI`ContextId

;

RecordModifierExpr :: rec : Expr
                      modifiers : seq1 of RecordModification
                      cid :- CI`ContextId

;

RecordModification :: field   : Name
                      newexpr : Expr
                      cid :- CI`ContextId
inv rm == len rm.field.ids = 1;

FieldSelectExpr :: rec : Expr
                   nm  : Name
                   cid :- CI`ContextId
inv n == len n.nm.ids = 1
           
;
\end{vdm_al}

\begin{vdm_al}
FctTypeInstExpr :: polyfct : Name
                   inst    : seq1 of Type
                   cid :- CI`ContextId  -- tp_info

;

LambdaExpr :: parm : seq of TypeBind
              body : Expr
              type : [ Type ]
              cid :- CI`ContextId

;

ApplyExpr :: fct : Expr
             arg : seq of Expr
             cid :- CI`ContextId

;

IsExpr :: type : BasicType | Name
          arg  : Expr
          cid :- CI`ContextId

;

UndefinedExpr::
          cid :- CI`ContextId
;

NarrowExpr :: expr : Expr
              type : Type
              cid :- CI`ContextId

;

TupleSelectExpr :: tuple : Expr
                   no    : NumLit | RealLit
                   cid :- CI`ContextId
;

TypeJudgementExpr :: expr : Expr
                     type : Type
                     cid :- CI`ContextId
;

PreConditionApplyExpr :: fct : Expr
                         arg : seq of Expr
                         cid :- CI`ContextId
;


\end{vdm_al}

\subsection{Names} \label{anames}
\begin{vdm_al}
Name :: ids : Ids
        cid :- CI`ContextId

;

Ids = seq of Id;

Id = seq of char;

NameList = seq1 of Name;

OldName :: ids : Ids
           cid :- CI`ContextId

;

\end{vdm_al}

\subsection{Statements} \label{astmt}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------------- Statements ---------------------------------
-----------------------------------------------------------------------

Stmt = DefStmt | LetStmt | LetBeSTStmt|
       AssignStmt | SeqForLoopStmt | AtomicAssignStmt |
       SetForLoopStmt | IndexForLoopStmt | WhileLoopStmt |
       ReturnStmt | IfStmt |
       CasesStmt | BlockStmt | IdentStmt |
       ErrorStmt | AlwaysStmt | CallStmt | SpecificationStmt |
       AssertStmt |
       ExitStmt | TrapStmt | RecTrapStmt | NonDetStmt ;

DefStmt :: value : seq1 of (PatternBind * Expr)
           In    : Stmt
           cid :- CI`ContextId
           ;

LetStmt :: localdef : seq1 of LocalDef
           In       : Stmt
           cid :- CI`ContextId
           ;

LetBeSTStmt :: bind : BindList
               St   : [Expr]
               In   : Stmt
               cid :- CI`ContextId
               ;

BlockStmt :: dcls  : seq of AssignDef
             stmts : seq of Stmt
             cid :- CI`ContextId
             ;

AssignDef :: var     : Name
             tp      : Type
             dclinit : [Expr]
             cid :- CI`ContextId
             ;

AtomicAssignStmt :: atm : seq1 of AssignStmt
                    cid :- CI`ContextId
                    ;

AssignStmt :: lhs : StateDesignator
              rhs : Expr
              cid :- CI`ContextId
              ;

StateDesignator = Name | FieldRef | MapOrSeqRef | NarrowRef;

FieldRef :: var : StateDesignator
            sel : Name
            cid :- CI`ContextId
            ;

MapOrSeqRef :: var : StateDesignator
               arg : Expr
               cid :- CI`ContextId
               ;

NarrowRef :: var : StateDesignator
             type : Type
             cid :- CI`ContextId
             ;

SeqForLoopStmt :: cv  : PatternBind
                  fseq : Expr
                  body : Stmt
                  cid :- CI`ContextId
                  ;

SetForLoopStmt :: cv   : Pattern
                  fset : Expr
                  body : Stmt
                  cid :- CI`ContextId
                  ;

IndexForLoopStmt :: cv   : Name
                    lb   : Expr
                    ub   : Expr
                    By   : [Expr]
                    body : Stmt
                    cid :- CI`ContextId
inv ifls == len ifls.cv.ids = 1;

WhileLoopStmt :: test : Expr
                 body : Stmt
                 cid :- CI`ContextId
                 ;

ReturnStmt :: val : [Expr]
              cid :- CI`ContextId
              ;

IfStmt :: test   : Expr
          cons   : Stmt
          elsif  : seq of ElseifStmt
          altn   : [Stmt]
          cid :- CI`ContextId
          ;

ElseifStmt :: test : Expr
              cons : Stmt
              cid :- CI`ContextId
              ;

Error :: nm     : Name
         cond   : Expr
         action : Expr
         cid :- CI`ContextId
         ;

ErrorStmt ::
          cid :- CI`ContextId
          ;

AlwaysStmt :: Post : Stmt
              body : Stmt
              cid :- CI`ContextId
              ;

TrapStmt :: pat : PatternBind
            Post : Stmt
            body : Stmt
            cid :- CI`ContextId
            ;

RecTrapStmt :: traps : seq1 of Trap
               body  : Stmt
               cid :- CI`ContextId
               ;

Trap :: match    : PatternBind
        trappost : Stmt
        cid :- CI`ContextId
        ;

ExitStmt :: expr : [Expr]
            cid :- CI`ContextId
            ;

NonDetStmt :: stmts : seq1 of Stmt
              cid :- CI`ContextId
              ;

CallStmt :: obj    : [Expr]
            oprt   : Name
            args   : seq of Expr
            cid :- CI`ContextId

;

CasesStmt :: sel    : Expr
             altns  : seq1 of CasesStmtAltn
             Others : [Stmt]
             cid :- CI`ContextId
             ;

CasesStmtAltn :: match : seq1 of Pattern
                 body  : Stmt
                 cid :- CI`ContextId
                 ;

IdentStmt ::
           cid :- CI`ContextId
           ;

SpecificationStmt :: opext    : seq of ExtInf
                     oppre    : [Expr]
                     oppost   : Expr
                     excps    : seq of Error
                     cid :- CI`ContextId
                     ;

AssertStmt :: expr : Expr
              cid :- CI`ContextId
              ;

\end{vdm_al}


\subsection{Patterns} \label{apattern}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- Patterns and Bindings ----------------------------
-----------------------------------------------------------------------

Pattern = PatternName | MatchVal | SetPattern | SeqPattern | MapPattern |
          TuplePattern | RecordPattern | MapletPattern
          ;

PatternName :: nm : [Name] | OldName
               tp : [Type]
               cid :- CI`ContextId

;
\end{vdm_al}

The $OldName$ type in $PatternName$ is used by the code generator, in
order to code generate post conditions on operations. Note, that old
names can not appear in pattern names in the concrete syntax.

\begin{vdm_al}
MatchVal :: val : Expr
            cid :- CI`ContextId
            ;

SetPattern = SetEnumPattern | SetUnionPattern;
\end{vdm_al}

The empty set is represented using $MatchVal$ and not
$SetEnumPattern$. Consequently $SetEnumPattern$ contains
$\seqof+{Pattern}$ instead of $\seqof*{Pattern}$.

\begin{vdm_al}
SetEnumPattern :: Elems : seq1 of Pattern
                  cid :- CI`ContextId

;

SetUnionPattern :: lp : Pattern
                   rp : Pattern
                   cid :- CI`ContextId
                   ;

SeqPattern = SeqEnumPattern | SeqConcPattern;
\end{vdm_al}

The empty sequence is represented using $MatchVal$ and not
$SeqEnumPattern$. Consequently $SeqEnumPattern$ contains
$\seqof+{Pattern}$ instead of $\seqof*{Pattern}$.

\begin{vdm_al}
SeqEnumPattern :: els : seq1 of Pattern
                  cid :- CI`ContextId

;

SeqConcPattern :: lp : Pattern
                  rp : Pattern
                  cid :- CI`ContextId
                  ;

MapPattern = MapEnumPattern | MapMergePattern;

MapletPattern :: dp : Pattern
                 rp : Pattern
                 cid :- CI`ContextId
                 ;

MapEnumPattern :: mls : seq1 of MapletPattern
                  cid :- CI`ContextId
                  ;

MapMergePattern :: lp : Pattern
                   rp : Pattern
                   cid :- CI`ContextId
                   ;


TuplePattern :: fields : seq1 of Pattern
                cid :- CI`ContextId

;

RecordPattern :: nm: Name
                 fields: seq of Pattern
                 cid :- CI`ContextId

\end{vdm_al}

\subsection{Bindings} \label{abinding}
\begin{vdm_al}
types

PatternBind = Pattern | Bind;

Bind = SetBind | TypeBind;

SetBind :: pat : Pattern
           Set : Expr
           cid :- CI`ContextId
           ;

SeqBind :: pat : Pattern
           Seq : Expr
           cid :- CI`ContextId
           ;

TypeBind :: pat : Pattern
            tp  : Type
            cid :- CI`ContextId
            ;

BindList = seq of MultBind;

MultBind = MultSetBind | MultTypeBind;

MultSetBind :: pat : seq1 of Pattern
               Set : Expr
               cid :- CI`ContextId
               ;

MultTypeBind :: pat : seq1 of Pattern
                tp  : Type
                cid :- CI`ContextId
                ;
\end{vdm_al}

\subsection{Literals} \label{aliterals}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- Literals -----------------------------------------
-----------------------------------------------------------------------

Literal = BoolLit | NilLit | NumLit | RealLit | CharLit | TextLit | QuoteLit;

BoolLit :: val : bool
           cid :- CI`ContextId
           ;

NilLit ::
        cid :- CI`ContextId
        ;

RealLit :: val : real
           cid :- CI`ContextId
           ;

--NumLit = RealLit
NumLit :: val : real
          cid :- CI`ContextId
inv n == is_int(n.val);

CharLit :: val : char
           cid :- CI`ContextId
           ;

TextLit :: val : seq of char
           cid :- CI`ContextId
           ;

QuoteLit :: val : seq1 of char
            cid :- CI`ContextId;
\end{vdm_al}

\begin{vdm_al}
MacroTp= <MACRO_FILE> | <MACRO_LINE> | <MACRO_COLUMN> | <MACRO_CLMOD> | <MACRO_FNOP>;

Macro :: tp : MacroTp
         cid :- CI`ContextId;
\end{vdm_al}

\subsection{Structure combining the AST and the ContextInfo} \label{AstCI}

{\em AstCI} is a structure that is not directly part of the AST. It
combines a {\em Document} or {\em Expr}, a {\em CI`TokenContextInfo\/}
and a {\em CI` FileIdMap} into a compound structure that is used by
all the test environments.

\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- AstCI --------------------------------------------
-----------------------------------------------------------------------

AstCI :: ast   : Document | Expr
         tllci : seq of CI`TokenContextInfo
         fidm  : CI` FileIdMap;
\end{vdm_al}

\subsection{Debugger Constructs} \label{debugconstr}

{\em \/} is not directly part of the AST but is so tightly
related to it that is has been defined as an AST node anyway. It is
the node corresponding to the \$\$ command in the debugger.

\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- Debugger Constructs ------------------------------
-----------------------------------------------------------------------

LastRes ::
        cid :- CI`ContextId
;

end AS
\end{vdm_al}

