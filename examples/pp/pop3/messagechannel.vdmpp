\section{The Message Channel Class}

\begin{vdm_al}
class MessageChannel

instance variables
\end{vdm_al}

\begin{vdm_al}--[MCInstvars]
instance variables
  data : [POP3Types`ClientCommand |
          POP3Types`ServerResponse]  := nil;
\end{vdm_al}

\begin{vdm_al}
instance variables

io : IO := new IO();
debug : bool := true;
\end{vdm_al}

\begin{vdm_al}--[MCSendListen]
operations

Send: POP3Types`ClientCommand | 
      POP3Types`ServerResponse ==> ()
Send(msg) ==
  data := msg;

Listen: () ==> POP3Types`ClientCommand | 
               POP3Types`ServerResponse
Listen() ==
let d = data in
  ( data := nil; return d
  );
\end{vdm_al}
 
\begin{vdm_al}--[MCServerSend]
operations

public ServerSend: POP3Types`ServerResponse ==> ()
ServerSend(p) == 
( if debug                                    --ignore
  then let - = io.echo("***> ServerSend")     --ignore
       in skip;                               --ignore
  Send(p);
  if debug                                    --ignore
  then let - = io.echo("***> fin ServerSend") --ignore
       in skip                                --ignore
);                                            --ignore
\end{vdm_al}

\begin{vdm_al}
public ClientListen: () ==> POP3Types`ServerResponse
ClientListen() == 
( if debug
  then let - = io.echo("***> ClientListen")
       in skip;
  let r = Listen(),
      - = if debug
          then io.echo("***> fin ClientListen")
          else false
  in
    return r;
);

public ClientSend: POP3Types`ClientCommand ==> ()
ClientSend(p) == 
( if debug
  then let - = io.echo("***> ClientSend")
       in skip;
  Send(p);
  if debug
  then let - = io.echo("***> fin ClientSend")
       in skip;
);

public ServerListen: () ==> POP3Types`ClientCommand
ServerListen() == 
( if debug
  then let - = io.echo("***> ServerListen")
       in skip;
  let c = Listen(),
      - = if debug
          then io.echo("***> fin ServerListen")
          else false
  in 
    return c
);
\end{vdm_al}

\begin{vdm_al}--[PPServerListen]
sync --ignore
  per ServerListen => #fin(ClientSend) - 1 = 
                      #fin(ServerListen);
\end{vdm_al}

\begin{vdm_al}--[PPClientListen]
  per ClientListen => #fin(ServerSend) - 1 = #fin(ClientListen);
\end{vdm_al}

\begin{vdm_al}--[PPServerSend]
  per ServerSend => #fin(ClientSend) = #fin(ServerListen) and
                    #fin(ServerListen) - 1 = #fin(ServerSend);
\end{vdm_al}

\begin{vdm_al}--[PPClientSend]
  per ClientSend => #fin(ServerSend) = #fin(ClientListen) 
                    and 
                    #fin(ClientSend) = #fin(ServerListen) 
                    and
                    #fin(ServerSend) = #fin(ClientSend) ;
\end{vdm_al}

\begin{vdm_al}
--  mutex(ServerSend, ServerListen);
--  mutex(ClientSend, ClientListen);
--  mutex(ServerSend);
--  mutex(ClientSend);

end MessageChannel
\end{vdm_al}
