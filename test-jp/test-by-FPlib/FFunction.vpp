\subsection{FFunction}
関数プログラミングに関わる関数を定義する。

\begin{vdm_al}
--"$Id"
class FFunction

functions 
\end{vdm_al}

Funtilは、ある条件ｐが真になるまで、初期値xに関数fを繰り返し適用する。
\begin{vdm_al}
static public Funtil[@T] : (@T -> bool) -> (@T -> @T) -> @T -> @T
Funtil(p)(f)(x) == if p(x) then x else Funtil[@T](p)(f)(f(x));
\end{vdm_al}

Fwhileは、ある条件ｐが真である間、初期値xに関数fを繰り返し適用する。
\begin{vdm_al}
static public Fwhile[@T] : (@T -> bool) -> (@T -> @T) -> @T -> @T
Fwhile(p)(f)(x) == if p(x) then Fwhile[@T](p)(f)(f(x)) else x;
\end{vdm_al}

Seqは、引数aに関数列fsの関数を連続適用する。
\begin{vdm_al}
static public Seq[@T] : seq of (@T -> @T) -> @T -> @T
Seq(fs)(a) ==
	cases fs :
	[hf] ^ tf	-> Seq[@T](tf)(hf(a)),
	[]		-> a
	end;

end FFunction
\end{vdm_al}


\begin{rtinfo}
[FFunction]{vdm.tc}[FFunction]
\end{rtinfo}