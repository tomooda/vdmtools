class JapaneseCalendar is subclass of Calendar
/*
責任
	日本の休日に関する情報を提供する。
概要
	日本の休日を返す。
	日付の有効期限は、スーパークラスCalendarの定義に従う。
	時刻に関わる計算は、日本標準時（JST)を使用している。
*/

values
	Rcsid = "$Id";
public 日本標準時とグリニッジ標準時との差 = 0.375;	-- 0.375 = 9 時間 = 9 / 24
public 西暦と和暦の年差 = 1988;

functions

static private 文字列変換補助関数: int -> seq of char
文字列変換補助関数(整数) == 
	let	str = Integer`asString	in
	if 整数 >= 10 then str(整数) else " " ^ str(整数);
	
static public 和暦日付文字列を得る : Date -> seq of char
和暦日付文字列を得る(西暦日付) == 
	let	asString =Integer`asString,
		和暦の年 = 西暦日付.年() - 西暦と和暦の年差,
		m = 西暦日付.月(),
		d = 西暦日付.日(),
		年文字列 = asString(和暦の年),
		月文字列 = 文字列変換補助関数(m),
		日付文字列 = 文字列変換補助関数(d)	in
		年文字列 ^ 月文字列 ^ 日付文字列
pre
	西暦日付.年() >= 西暦と和暦の年差;	--今のところ、平成以後の和暦日付文字列を得る

operations

public 休日集合を設定する: int ==> ()
休日集合を設定する(年) ==
	let	成人の日 = 第n指定曜日を得る(年,1,2,<月>),
		海の日 = if 年 >= 2003 then 第n指定曜日を得る(年,7, 3,<月>) else 整数三つ組から日付を得る(年,7,20),
		敬老の日 = if 年 >= 2003 then 第n指定曜日を得る(年,9, 3,<月>) else 整数三つ組から日付を得る(年,9,15),
		体育の日 = 第n指定曜日を得る(年,10, 2,<月>),
		国民の休日 =  {
			整数三つ組から日付を得る(年,1,1), 
			成人の日,
			整数三つ組から日付を得る(年,2,11),
			春分を得る(年), 
			整数三つ組から日付を得る(年,4,29),
			整数三つ組から日付を得る(年,5,3),
			整数三つ組から日付を得る(年,5,4), 	--formally this date is not national holiday
			整数三つ組から日付を得る(年,5,5),
			海の日,
			敬老の日,
			秋分を得る(年),
			体育の日,
			整数三つ組から日付を得る(年,11,3),
			整数三つ組から日付を得る(年,11,23),
			整数三つ組から日付を得る(年,12,23)
		},
		振替休日 = 
			if 年 >= 2007 then 
				{未来の国民の休日以外の日を得る(国民の休日, 日) | 日 in set 国民の休日 & 日曜日か？(日)}
			else
				 {日.＋(1) | 日 in set 国民の休日 & 日曜日か？(日)},
		休日に挟まれた平日 = 
			if 年 >= 2007 then
				休日に挟まれた平日を得る(国民の休日) 
			else
				{}
	in
	休日集合写像 := 休日集合写像 munion { 年 |-> 国民の休日 union 振替休日 union 休日に挟まれた平日}
pre
	年 >= 2000;	--今のところ、2000年以後の休日のみ考えている。

public JapaneseCalendar : () ==> JapaneseCalendar
JapaneseCalendar() ==
	(
	グリニッジ標準時との差を設定する(日本標準時とグリニッジ標準時との差); 
	return self
	);

public 休日に挟まれた平日を得る : set of Date ==> set of Date
休日に挟まれた平日を得る(a国民の休日) == (
	let 
		休日に挟まれた平日の候補 = 
			dunion { {日.−(1), 日.＋(1)} | 日 in set a国民の休日 &
				日.−(1).年() = 日.年() and 日.＋(1).年() = 日.年()},
		国民の休日に挟まれた平日 = 
			{ 日 | 日 in set 休日に挟まれた平日の候補 & 
				let 前日 : Date = 日.−(1), 翌日 : Date =  日.＋(1) in
				日付集合に含まれる(前日, a国民の休日) and 日付集合に含まれる(翌日, a国民の休日)}
	in
	return 国民の休日に挟まれた平日
 );

functions
--国民の休日でない日付を返す（未来へ向かって探索する）
public 未来の国民の休日以外の日を得る :set of Date * Date-> Date
未来の国民の休日以外の日を得る(a国民の休日, 日付) ==
	cases  日付集合に含まれる(日付, a国民の休日) :
		(true)	-> 未来の国民の休日以外の日を得る(a国民の休日, 日付.＋( 1)),
		others	-> 日付
	end
measure 未来の国民の休日以外の日を得るMeasure;

未来の国民の休日以外の日を得るMeasure : set of Date * Date +> nat
未来の国民の休日以外の日を得るMeasure(a国民の休日, date) == 
	cases 日付集合に含まれる(date, a国民の休日) :
	(true) 	-> -date.修正ユリウス日獲得(),
	others	-> 0
	end;

end JapaneseCalendar